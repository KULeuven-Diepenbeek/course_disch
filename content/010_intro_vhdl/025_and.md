---
title: 'AND poort'
pre: "<i class='fas fa-book'></i> "
chapter: false
weight: 25
draft: false
---

Heb géén schrik van deze titel. We gaan zeker niet alle componenten 1-voor-1 afgaan :smiley:.

Uiteraard gaan design moeilijk te beschrijven zijn in 1 bestand. Componenten kunnen hergebruikt worden in andere componenten. Laat ons als voorbeel een AND poort bouwen met behulp van de eerder geziene **NAND** poort en de **invertor**. het schema van ons design ziet er dus al volgt uit.

{{% figure src="/images/intro/AND_gate_NAND_NOT.svg" title="AND gate"  %}}


{{% multiHcolumn %}}
{{% column %}}
{{< highlight vhdl >}}
-- gebruik de package STD_LOGIC_1164 uit de library IEEE
library IEEE;
    use IEEE.STD_LOGIC_1164.ALL;

-- poorten van de component
entity and_gate is
    Port (
        ingang_A : in std_logic; 
        ingang_B : in std_logic;
        uitgang_Z : out std_logic
    );
end and_gate;

architecture Behavioral of and_gate is

    -- declaraties van componenten die gebruikt worden
    component nand_gate is
      Port (
          A : in std_logic; 
          B : in std_logic;
          Y : out std_logic
      );
    end component;

    component invertor is
        Port (
            A: in std_logic;
            Z: out std_logic
        );
    end component;

    -- declaratie van signalen die IN het design gebruikt worden
    signal componenten_verbindings_draad : std_logic;

begin

    mijn_mooie_NAND: component nand_gate port map(
        A => ingang_A,
        B => ingang_B,
        Y => componenten_verbindings_draad
    );

    mijn_ijskoude_invertor: component invertor port map(
        A => componenten_verbindings_draad,
        Z => uitgang_Z
    );

end Behavioral;
{{< /highlight >}}
{{% /column %}}
{{% column %}}
De beschrijving van deze AND poort begint met het gebruiken van een **package** (STD_LOGIC_1164) uit een **library** (IEEE).

Vervolgens is er weer de **entity** block die aangeeft dat dit design 2 ingangspinnen heeft en 1 uitgangspin.

In de architecture verschijnt er iets nieuws. Tussen de **architecture** en de **begin** kunnen er declaraties gebeuren. Dit kunnen zowel declaraties zijn van signalen als van compontenten.

Componenten zijn de entities van andere design die gebruikt worden. Volgens ons schema gaan we gebruik maken van een NAND poort van en van invertor. In deze component declaraties geven we aan wat de *behuizing* is van de componenten die we gebruiken.

{{% notice tip %}}
Merk op dat een component exact hetzelfde geschreven wordt als de entity van de component zelf. Vervang het wordje *entity* door *component*; en eindig met "end *component*" in plaats van "end &lt;entity_name>"
{{% /notice %}}

Signalen kun je zien als "draadje op een breadboard". In de beschrijving van de *and_gate* is er 1 signal gedeclareerd: **componenten_verbindings_draad**. Zoals je merkt, ben je vrij om je signalen een naam te geven.

Tenslotte bevat het design tussen de begin en end van de architecture de beschrijving van het gedrag van de component. Hier worden er twee **instantiaties** gemaakt, 1 van elke component. Een instantiatie mag je ook zelf benoemen (bv: mijn_mooie_NAND en mijn_ijskoude_invertor). Na de naamgeving en de specificatie van de component begint de **port mapping**.

<center><b>LINKS => RECHTS (komma)</b></center>
Voor iedere poort van de component schrijven we links de naam van de poort. Hierachter komen symbolen **=>** waarna vervolgens de signaalnaam komt van het huidige design. Zoals je kan zien in het voorbeeld, kun je **RECHTS** ook een poortnaam schrijven van de entity die we aan het beschrijven zijn.

{{% /column %}}
{{% /multiHcolumn %}}

Met dit voorbeeld wordt er weergegeven hoe je component instantiatie kan doen. Uiteraard kan je ook een de **nand_gate.vhd** file kopiëren en de regel ```Y <= A NAND B;``` vervangen door ```Y <= A AND B;```.

## Alle lijnen ... tegelijk

In tegenstelling tot alle software die jullie tot hier toe geschreven hebben, is er 1 heel belangrijk verschil met deze hardware beschrijving. Iedere lijn tussen de *begin* en *end* van de architecture wordt **TEGELIJKERTIJD** uitgevoerd. Het kan uiteraard (zoals in dit voorbeeld) het resultaat van één blok afhangt van het resultaat van een ander blok, maar zowel de NAND-gate als de invertor zijn tegelijkertijd aan het werken.

Denk nog eens terug aan de beschrijving op de MUDEC. Het IC met de AND poort werkt ook simultaan met het IC met de invertor.